\section{The Univalence Axiom and some consequences}\label{sec:hlevels}

In this section we will describe a number of constructions and results
which are more closely related to the univalent approach.  Because it
would take us to far afield in an introductory paper such as this, we
will merely mention a number of the results and display some of the
corresponding Coq code.  That is to say, the development given here is
not self contained: there are many definitions and lemmas we do not
give that would be required in order to obtain all of the results
described here.

\subsection{An alternative characterization of the Univalence Axiom}

Before giving the explicit statement of the Univalence Axiom, we will
first require a map which turns a path in the universe \verb|UU|
into a weak equivalence.  For a change of pace, we give a direct
definition of this map as follows:
\begin{center}
  \begin{coqcode}
Definition eqweqmap { A B : UU } ( p : paths A B ) : weq A B 
  := match p with idpath => ( idweq _ ) end.
  \end{coqcode}
\end{center}
That is, \verb|eqweqmap| is the map from the path space
\verb|paths A B| to the space \verb|weq A B| of weak
equivalences induced by the operation of sending the identity path on
\verb|A| to the identity weak equivalence \verb|idweq A| on 
\verb|A|.

We then define the type 
\begin{center}
  \begin{coqcode}
Definition isweqeqweqmap := forall A B : UU, isweq (@eqweqmap A B).
  \end{coqcode}
\end{center}
The \emph{Univalence Axiom} then states that there is a term of type
\verb|isweqeqweqmap|.  However, before explicitly adding the
Univalence Axiom as an assumption, we would first like to give a
logically equivalent version of this principle.  The idea behind
this equivalent form of the Univalence Axiom, which can be seen easily
by considering the semantic version of the Univalence Axiom (i.e.,
what the axiom says in terms of models), states that the type of weak
equivalences is inductively generated by the terms of the form
\verb|idweq A|.
In particular, they are inductively generated by these terms in the
same way that the path space construction \verb|paths| is
inductively generated by the identity paths.  Formally, we define the
type
\begin{center}
  \begin{coqcode}
Definition weqindelim := 
forall E : 
total ( fun x : UU => total ( fun y : UU => weq x y ) ) -> UU,
forall p : ( forall x : UU, E ( pair x ( pair x ( idweq x ) ) ) ), 
forall x y : UU, forall z : weq x y, E ( pair x ( pair y z ) ).
  \end{coqcode}
\end{center}
Intuitively, let $B$ be the space with points given by the following
data:
\begin{itemize}
\item small spaces $X$ and $Y$;
\item weak equivalences $f$ from $X$ to $Y$.
\end{itemize}
Then, for a fibration $E\to B$ over $B$, if there exists a proof $p$ (term)
that each fiber $E_{1_{X}}$ over identity weak equivalences $1_{X}$ is
inhabited, then a term of type $\texttt{weqindelim}\;E\;p$ yields a proof
that \emph{every} fiber $E_{f}$ is inhabited.  Thinking of $E$ as a
``property'' of weak equivalences, this states that in order to prove
that a property (definable type theoretically) of weak equivalences holds it
suffices to prove that the property holds of identity weak
equivalences.  We refer to this as \emph{induction on weak equivalences}.

In order to prove that if the Univalence Axiom holds, then the
induction principle \verb|weqindelim| also holds we make use of
the following lemma, the proof of which is immediate.
\begin{center}
  \begin{coqcode}
Lemma weqind0 
( E : total (fun x : UU => total ( fun y : UU => weq x y ) ) -> UU) 
( p : ( forall x : UU, E ( pair x ( pair x ( idweq x ) ) ) ) ) : 
(forall x y : UU,
  forall z : paths x y, E ( pair x ( pair y ( eqweqmap z ) ) )).    
  \end{coqcode}
\end{center}
This lemma states that the induction principle in question holds for
weak equivalences of the form $\texttt{eqweqmap}(f)$ for $f$ a path
between small spaces.  Using this, we obtain the following:
\begin{center}
  \begin{coqcode}
Definition weqind ( univ : isweqeqweqmap ) : weqindelim := 
fun E p A B f => 
 transportf ( fun z => E ( pair A ( pair B z ) ) )
  ( weqeqmaplinv univ f ) ( weqind0 E p A B (weqeqmap univ f) ). 
  \end{coqcode}
\end{center}
Here we are assuming that the Univalence Axiom holds.
I.e., that there is a term \verb|univ| of type
\verb|isweqeqweqmap|.  Then, given all of the data of the
induction principle, in order to obtain a proof that the fiber $E_{f}$
is inhabited we observe that, by \verb|weqind0|, there exists a
term $e$ in the 
\begin{align*}
  E_{\texttt{eqweqmap}(\texttt{weqeqmap}(f))},
\end{align*}
where \verb|weqeqmap| is the homotopy inverse of 
\verb|eqweqmap|, which exists by the fact that we are assuming
the Univalence Axiom.  It then suffices to transfer $e$ along the path
from $\texttt{eqweqmap}(\texttt{weqeqmap}(f))$ to $f$ (here called
\verb|weqeqmaplinv|). 

We note that, when the Univalence Axiom holds, the following
computation principle corresponding to \verb|weqindelim| also holds:
\begin{center}
  \begin{coqcode}
Definition weqindcomp ( rec : weqindelim ) := 
forall  E :
 total ( fun x : UU => total ( fun y : UU => weq x y ) ) -> UU, 
forall p : ( forall x : UU, E ( pair x ( pair x ( idweq x ) ) ) ), 
forall x : UU, paths ( rec E p x x ( idweq x ) ) ( p x ).
  \end{coqcode}
\end{center}
Explicitly, we have the following Theorem:
\begin{center}
  \begin{coqcode}
Theorem weqcomp (univ : isweqeqweqmap) : weqindcomp ( weqind univ ).
  \end{coqcode}
\end{center}
The proof of this is slightly involved and we leave the details to the
reader (they can also be found in the companion Coq file for this
tutorial).  We refer to this computation principle as the
\emph{computation principle for weak equivalences}.

It turns out that the converse implications also hold: in order to
prove that the Univalence Axiom holds, it suffices to show that there
are terms \verb|rec : weqindelim| and 
\verb|reccomp : weqindcomp rec|.  That is, we have the following
Theorem:
\begin{center}
\begin{coqcode}
Theorem univfromind {rec : weqindelim} ( reccomp : weqindcomp rec ) 
 : isweqeqweqmap. 
\end{coqcode}
\end{center}
In order to prove this, it suffices, by the Grad Theorem, to prove
that \verb|eqweqmap| has a homotopy inverse.  To construct the
homotopy inverse we employ induction on weak equivalences.  That
is, to construct a map from the space \verb|weq A B| to the path
space \verb|paths A B| it suffices to be able to specify the
image of an identity weak equivalence.  But these are clearly sent to the
identity path.  The fact that this determines a homotopy inverse is
then a consequence of the computation principle for weak
equivalences.  (Full details of the proof can be found in the Coq file
accompanying this paper.)

\subsection{Function extensionality}

Henceforth, we assume that the Univalence Axiom holds.  That is, we
adopt the following:
\begin{center}
  \begin{coqcode}
Axiom univ : isweqeqweqmap.
  \end{coqcode}
\end{center}
In Coq, the command \verb|Axiom| serves to introduce a new global
hypothesis.  In this case, we assume given a proof \verb|univ| of
the Univalence Axiom.  Note that, although a good number do, not all
of the facts we prove below require the Univalence Axiom for their
proofs.  We will begin by briefly summarizing one of Voevodsky's type
theoretic results regarding the Univalence Axiom.

One somewhat curious feature of type theory without the Univalence
Axiom is that the principle of Function Extensionality is not
derivable.  Although it can be formulated in a number of ways, the
principle of Function Extensionality should be understood as stating
that, for continuous maps $f,g\colon A\to B$, paths from $f$ to $g$ in the
function space $B^{A}$ correspond to homotopies from $f$ to $g$.

Voevodsky \cite{Vo2012a} showed that Function Extensionality (and a
number of closely related principles) is a consequence of the
Univalence Axiom.  A sketch of the proof written in the usual
mathematical style can be found in Gambino \cite{Gambino:OR}.  We will
not describe the proof of Function Extensionality here.  We merely
mention that we will make use of it in what follows.  Explicitly, we
make use of a term 
\begin{center}
  \begin{coqcode}
funextsec : forall B : UU, forall E : B -> UU, 
 forall f g : ( forall x : B, E x ), 
  isweq ( pathtohtpysec f g ).
  \end{coqcode}
\end{center}
This is a slightly more general form of Function Extensionality and
implies the more common version as follows:
\begin{center}
  \begin{coqcode}
Definition funextfun { A B : UU } ( f g : A -> B ) : 
homot f g -> paths f g 
 := weqinv ( pair _ ( funextsec A ( fun z => B ) f g ) ).
  \end{coqcode}
\end{center}

\subsection{Impredicativity of h-levels}

We will now describe a number of consequences of the Univalence Axiom
concerning h-levels.  First, we give the definition of h-levels as follows:
\begin{center}
  \begin{coqcode}
Fixpoint isofhlevel ( n : nat ) ( A : UU ) := 
  match n with
    | O => iscontr A 
    | S n => forall a b : A, isofhlevel n ( paths a b )
  end.
  \end{coqcode}
\end{center}
Here the operation \verb|Fixpoint| tells Coq that we will define
a function out of an inductive type (in this case \verb|nat|)
recursively.  So, the definition is saying that a type $A$ is of
h-level 0 when it is contractible and it is of h-level $(n+1)$ when,
for all points $a$ and $b$ of $A$, the path space
$\texttt{paths}\;a\;b$ is of h-level $n$.

The h-levels satisfy an important property which type theorists refer
to as being \emph{impredicative}: they are closed under universal
quantification in the sense described below.  In the base case (for contractible
spaces), this is proved as follows:
\begin{center}
  \begin{coqcode}
Lemma impredbase { B : UU } ( E : B -> UU ) : 
(forall x : B, iscontr ( E x )) -> iscontr (forall x : B, E x).
  \end{coqcode}
\end{center}
Intuitively, what this says is that given a fibration $E$ over $B$, if
every fiber $E_{x}$ is contractible, then the space 
\verb|forall x : B, E x | of sections of the fibration is also
contractible.  We omit the proof, which is an immediate consequence of
\verb|funextsec|.

The following general principle of impredicativity of h-levels then
follows by induction:
\begin{center}
  \begin{coqcode}
Lemma impred ( n : nat ) : forall B : UU, forall E : B -> UU, 
( forall x : B, isofhlevel n ( E x ) ) 
    -> isofhlevel n ( forall x : B, E x ).
  \end{coqcode}
\end{center}
Again, this lemma states that if all fibers $E_{x}$ are of h-level
$n$, then so is the space of sections of the fibration.

\subsection{The total space and h-levels}

Next, we would like to explain the behavior of h-levels when it comes
to forming the total space of a fibration.  Assume given a fibration
$E\to B$ over $B$.  That is, we assume given a term 
\verb|E : B -> UU |.  Then, for any points $x$ and $y$, there is
a weak equivalence between the path space $\texttt{paths}\;x\;y$ and
the space which consists of pairs $(f,g)$ consisting of paths $f$ from $\pi_{1}(x)$ to
$\pi_{1}(y)$ and paths $g$ from $f_{!}(\pi_{2}(x))$ to $\pi_{2}(y)$
(see Section \ref{sec:transport} above for more on this idea).  Using this fact we obtain the
following lemma:
\begin{center}
  \begin{coqcode}
Lemma totalandhlevel ( n : nat ) : forall B : UU, 
forall E : B -> UU, forall is : isofhlevel n B, 
forall is' : ( forall x : B, isofhlevel n ( E x ) ), 
isofhlevel n ( total E ).   
  \end{coqcode}
\end{center}
The lemma states that if the base space $B$ and all fibers $E_{x}$ are
of h-level $n$, then so is the total space.  In the base case $n=0$
this is straightforward.  In the induction case, we observe that, for
any $x$ and $y$ in the total space,
\begin{center}
  \begin{coqcode}
isofhlevel n ( paths x y )
  \end{coqcode}
\end{center}
can be replaced, by the Univalence Axiom and the weak equivalence
mentioned above, by 
\begin{center}
  \begin{coqcode}
isofhlevel n ( 
total ( fun v : paths ( pr1 x ) ( pr1 y ) => 
paths ( transportf E v ( pr2 x ) ) ( pr2 y ) ) ).
  \end{coqcode}
\end{center}
But the induction hypothesis applies in this case, since we are now
dealing with a space of the form \verb|total ...|, and so we are done.

\subsection{The unit type and contractibility}

The unit type \verb|unit| corresponds to the terminal object $1$
in the category of spaces under consideration.  It is the inductive
type with a single generator \verb|tt : unit|.  For any type 
\verb|A| there is an induced map
\begin{center}
  \begin{coqcode}
tounit A : A -> unit.
  \end{coqcode}
\end{center}
It is a useful fact about contractible spaces that \verb|A| is
contractible if and only if \verb|tounit A| is a weak
equivalence.  We omit the straightforward proof of this equivalence.  

One fact about contractible spaces we will require is the fact that if
\verb|A| is contractible, then so is the type 
\verb|iscontr A| of proofs that \verb|A| contractible. This
is captured by the following lemma:
\begin{center}
  \begin{coqcode}
Lemma iscontrcontr { A : UU } ( is : iscontr A ) : 
iscontr ( iscontr A ).    
  \end{coqcode}
\end{center}
By the Univalence Axiom and the characterization of contractible
spaces mentioned above, in order to prove this theorem it suffices to
consider the case where \verb|A| is the unit type, which is
more or less immediate.  This proof reveals one important method for
using the Univalence Axiom: to prove something about a space $A$ it
suffices, by the Univalence Axiom, to prove the same fact about an
easier to manage space which is weakly equivalent to $A$.

\subsection{Some propositions}

We think of types of h-level 1 as being \emph{propositions} (or
truth-values) in the sense familiar to logicians.  Following this
intuition, we introduce the following notation:
\begin{center}
  \begin{coqcode}
Notation isaprop := ( isofhlevel 1 ).
  \end{coqcode}
\end{center}
Being a proposition is the same as being \emph{proof irrelevant}.
That is, $P$ is a proposition if and only if, for all terms $p,q:P$, there is
a path from $p$ to $q$.

One important consequence of \verb|iscontrcontr| is the fact that
being contractible is itself a proposition:
\begin{center}
  \begin{coqcode}
Lemma isapropiscontr ( A : UU ) : isaprop ( iscontr A ).
  \end{coqcode}
\end{center}
First, note that it is clear that a sufficient condition for being a
proposition is being contractible.  So, given points $p$ and $q$ of
type \verb|iscontr A|, it suffices to show that the type 
\verb|iscontr A| is itself contractible, which is by
\verb|iscontrcontr|.

As a consequence of impredicativity of h-levels together with
\verb|isapropiscontr| we obtain the following lemma:
\begin{center}
  \begin{coqcode}
Lemma isapropisweq { A B : UU } ( f : A -> B ) : isaprop ( isweq f ).
  \end{coqcode}
\end{center}
Again using impredicativity of h-levels and \verb|isapropisweq|
we obtain the following theorem:
\begin{center}
  \begin{coqcode}
Theorem isaxiomunivalence : isaprop ( isweqeqweqmap ).
  \end{coqcode}
\end{center}
That is, the type of the Univalence Axiom is a proposition and
therefore, assuming that there exists a term \verb|univ| of this
type, the space of such terms is contractible.

Similarly, a straightforward argument shows that, for any space $A$,
the type \verb|isofhlevel n A| is a proposition.

\subsection{The h-levels of h-universes}

We will now consider types of the form
\begin{center}
  \begin{coqcode}
total ( fun x : UU => isofhlevel n x )
  \end{coqcode}
\end{center}
which correspond to the types of all small spaces of a fixed
h-level $n$.  That is, they are what you might call
\emph{h-universes}.  Ultimately we will compute the h-levels of
h-universes.  First we will develop some further basic facts
about h-levels.

Note that if $A$ is of h-level $n$ then a straightforward argument
(using the discussion of h-levels of total spaces above), shows that
$A$ is also of h-level $(n+1)$.  That is, the h-universes are
cumulative.  Next, observe that if $B$ is of h-level $(n+1)$, then so
is the space of weak equivalences $\texttt{WEq}\;A\;B$ for any space
$A$:
\begin{center}
  \begin{coqcode}
Lemma hlevelweqcodomain ( n : nat ) : forall A B : UU, 
isofhlevel ( S n ) B -> isofhlevel ( S n ) ( weq A B ).
Proof.
  intros A B is. apply totalandhlevel. apply impred. 
  intros. apply is. intros. apply isaproptoisofhlevelSn. 
  apply isapropisweq.
Defined.
  \end{coqcode}
\end{center}
The proof is as follows.  It suffices, by 
\verb|totalandhlevel|, to prove separately that both the function
space \verb|A -> B| and the space of proofs that an element $f$ of
the function space is a weak equivalence are of h-level $(n+1)$.  The
former is by impredicativity of h-levels and the latter is by 
\verb|isapropisweq|.

Now, for each fixed $n$, there is a version of \verb|eqweqmap|
relativized to the h-universe of type of h-level $n$:
\begin{center}
  \begin{coqcode}
Definition hlevelneqweqmap ( n : nat ) 
( A B : total ( fun x : UU => isofhlevel n x ) ) 
: paths A B -> weq ( pr1 A ) ( pr1 B ) := 
fun f => eqweqmap ( pathintotalfiberpr1 f ) .
  \end{coqcode}
\end{center}
It turns out that, because \verb|isweq f| is a proposition, this
map is a weak equivalence:
\begin{center}
  \begin{coqcode}
Lemma isweqhlevelneqweqmap ( n : nat ) : 
forall A B : total ( fun x : UU => isofhlevel n x ), 
isweq ( hlevelneqweqmap n A B ).
  \end{coqcode}
\end{center}
Next, we observe that if both $A$ and $B$ are contractible, then so is
the space of weak equivalences from $A$ to $B$:
\begin{center}
\begin{coqcode}
Lemma iscontrandweq { A B : UU } ( is : iscontr A ) 
 ( is' : iscontr B ) : iscontr ( weq A B ).
\end{coqcode}
\end{center}
Finally, we observe that the h-universe of types of h-level $n$ itself
has h-level $(n+1)$:
\begin{center}
  \begin{coqcode}
Theorem isofhlevelSnhn ( n : nat ) : isofhlevel ( S n ) 
( total ( fun x : UU => isofhlevel n x ) ).
  \end{coqcode}
\end{center}
The proof is as follows.  First, note that if we are given $A$ and $B$
of type 
\begin{center}
  \begin{coqcode}
total ( fun x : UU => isofhlevel n x ),
  \end{coqcode}
\end{center}
then these terms should themselves be thought of as pairs $A=(A,p)$ and
$B=(B,q)$ where $p$ is a proof that $A$ is of h-level $n$ and $q$ is a
proof that $B$ is of h-level $n$.  Nonetheless, there is a weak
equivalence between the type $\texttt{paths}\;(A,p)\;(B,q)$ and the
type $\texttt{WEq}\;A\;B$ and as such it suffices to construct a
term of type
\begin{center}
  \begin{coqcode}
isofhlevel n ( weq A B ).
  \end{coqcode}
\end{center}
When $n=0$ this is by \verb|iscontrandweq| and in the case where
$n=m+1$ it is by \verb|hlevelweqcodomain|.